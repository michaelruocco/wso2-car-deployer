plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.7.1'
    id 'com.github.ben-manes.versions' version '0.13.0'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

repositories {
    mavenCentral()
    maven { url 'http://dist.wso2.org/maven2/' }
}

dependencies {
    compile group: 'org.wso2.carbon', name: 'org.wso2.carbon.authenticator.stub', version: '4.2.0'
    compile group: 'org.wso2.developerstudio', name: 'org.wso2.developerstudio.eclipse.carbonserver.base', version: '3.6.0'
    compile group: 'org.wso2.carbon', name: 'org.wso2.carbon.application.mgt.stub', version: '4.2.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.1'
    testCompile group: 'eu.codearte.catch-exception', name: 'catch-exception', version: '1.4.4'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '2.0.2-beta'
    testCompile group: 'org.testcontainers', name: 'testcontainers', version: '1.1.6'
    testCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.22'
    testCompile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.22'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    systemProperty "javax.net.ssl.trustStore", "$projectDir/truststore/cacerts"
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(uploadArchives)) {
        signing {
            required true
            sign configurations.archives
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                packaging 'jar'
                name 'WSO2 Car Deployer'
                description 'Library to abstract deploying car files into WSO2'
                groupId 'com.github.michaelruocco'
                artifactId 'wso2-car-deployer'
                version '1.0.0'
                url 'https://github.com/michaelruocco/wso2-car-deployer'

                developers {
                    developer {
                        id 'mruoc'
                        name 'Michael Ruocco'
                        email 'michael.ruocco@hotmail.com'
                    }
                }

                scm {
                    connection 'scm:git:git@github.com/michaelruocco/wso2-car-deployer.git'
                    developerConnection 'scm:git:git@github.com/michaelruocco/wso2-car-deployer.git'
                    url 'git@github.com/michaelruocco/wso2-car-deployer.git'
                }

                licenses {
                    license {
                        name 'BSD License'
                        url 'http://www.opensource.org/licenses/bsd-license.php'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}